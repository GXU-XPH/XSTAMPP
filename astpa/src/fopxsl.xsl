<?xml version="1.0" encoding="ISO-8859-1"?>

<xsl:stylesheet version="1.0" xmlns:fo="http://www.w3.org/1999/XSL/Format"
	xmlns:xsl="http://www.w3.org/1999/XSL/Transform">
	<!-- author: Jaqueline Patzek, Patrick Wickenhäuser -->
	<!-- StuPro 2013 / 2014 -->
	
	<!-- Note: This xsl gathers all the informations from the .haz-file -->
	<!-- which is generated by the "prepareForExport()"-Command in the  -->
	<!-- ViewContainer.java -->
	
	<xsl:template match="/*">
		<fo:root>
			<!-- Page layout -->
			<fo:layout-master-set>

				<!-- Page-Master for the front-page -->
				<fo:simple-page-master margin="5mm 5mm 10mm 5mm"
					page-height="297mm" page-width="210mm" master-name="A4">
					<fo:region-body margin="15mm 0mm 10mm 5mm" />
					<fo:region-before extent="20mm" display-align="before" />
					<fo:region-after extent="10mm" display-align="after" />
				</fo:simple-page-master>

				<!-- Page-Master for the common pages -->
				<fo:simple-page-master margin="5mm 5mm 5mm 5mm"
					page-height="297mm" page-width="210mm" master-name="titel">
					<fo:region-body margin="5mm 0mm 10mm 5mm" />
				</fo:simple-page-master>

			</fo:layout-master-set>
			
			<!-- ++++++++++++++++++++++++++++++++++++++++++++++++++++++ -->
			<!-- ++++++++++++++++++++ START OF PDF ++++++++++++++++++++ -->
			<!-- ++++++++++++++++++++++++++++++++++++++++++++++++++++++ -->
			
			<!-- +++++ Front-Page +++++ -->
			<fo:page-sequence master-reference="titel">
				<fo:flow flow-name="xsl-region-body">
					<fo:block intrusion-displace="line">
						<fo:table space-after="30pt">
							<fo:table-column column-number="1" column-width="100%"
								border-style="none" />
							<fo:table-body>
								<fo:table-row>
									<fo:table-cell padding="10px" text-align="center">
										<fo:block font-size="18pt">
											A-STPA-Report
										</fo:block>
									</fo:table-cell>
								</fo:table-row>
								<fo:table-row>
									<fo:table-cell background-color="#1A277A" padding="10px"
										text-align="center">
										<xsl:call-template name="headTheme"/>
										<fo:block font-size="24pt" color="#FFFFFF">
											<xsl:call-template name="fontTheme"/>
											<xsl:value-of select="projectdata/projectName" />
										</fo:block>
									</fo:table-cell>
								</fo:table-row>
								<xsl:if test="exportinformation/logoPath">
								<fo:table-row>
									<fo:table-cell padding="15px" text-align="center">
										<fo:block>		
											<fo:external-graphic content-width="100mm">
												<xsl:attribute name="src">
												<!-- Path of the logo via haz-file -->
												<xsl:value-of select="exportinformation/logoPath" />
												</xsl:attribute>
											</fo:external-graphic>
											</fo:block>
									</fo:table-cell>
								</fo:table-row>
								</xsl:if>
							</fo:table-body>
						</fo:table>
					</fo:block>
					
					<!-- Main-data for the front-page -->
					<fo:block intrusion-displace="line">
						<fo:table space-after="30pt" margin="5mm 5mm 10mm 5mm">
							<fo:table-column column-number="1" column-width="25%"
								border-style="none" />
							<fo:table-column column-number="2" column-width="75%"
								border-style="none" />
							<fo:table-body>
								<fo:table-row border="1pt solid black">
									<fo:table-cell padding="4px">
										<fo:block font-size="12pt" font-weight="bold">
											Title
										</fo:block>
									</fo:table-cell>
									<fo:table-cell padding="4px">
										<fo:block font-size="12pt">
											<xsl:value-of select="projectdata/projectName" />
										</fo:block>
									</fo:table-cell>
								</fo:table-row>
								<fo:table-row border="1pt solid black">
									<fo:table-cell padding="4px">
										<fo:block font-size="12pt" font-weight="bold">
											Date and Time
										</fo:block>
									</fo:table-cell>
									<fo:table-cell padding="4px">
										<fo:block font-size="12pt">
											<xsl:value-of select="exportinformation/date" />
										</fo:block>
									</fo:table-cell>
								</fo:table-row>
								<xsl:if test="exportinformation/company">
									<fo:table-row border="1pt solid black">
										<fo:table-cell padding="4px">
											<fo:block font-size="12pt" font-weight="bold">
												Company
											</fo:block>
										</fo:table-cell>
										<fo:table-cell padding="4px">
											<fo:block font-size="12pt">
												<xsl:value-of select="exportinformation/company" />
											</fo:block>
										</fo:table-cell>
									</fo:table-row>
								</xsl:if>
								<fo:table-row border="1pt solid black">
									<fo:table-cell padding="4px">
										<fo:block font-size="12pt" font-weight="bold">
											Description
										</fo:block>
									</fo:table-cell>
									<fo:table-cell padding="4px">
										<fo:block font-size="12pt">
											<xsl:value-of select="projectdata/projectDescription" />
										</fo:block>
									</fo:table-cell>
								</fo:table-row>
							</fo:table-body>
						</fo:table>
					</fo:block>
				</fo:flow>
			</fo:page-sequence>

			<!-- +++++ Common Page content +++++ -->
			<fo:page-sequence master-reference="A4" id="total">
				<!-- Header-Block -->
				<fo:static-content flow-name="xsl-region-before">
					<fo:table background-color="#DEDEDE">
						<fo:table-body>
							<fo:table-row>
								<fo:table-cell padding="4px">
									<fo:block font-size="10pt">
										<xsl:value-of select="projectdata/projectName" />
									</fo:block>
								</fo:table-cell>
								<fo:table-cell padding="4px">
									<fo:block font-size="10pt" text-align="right">
										<xsl:value-of select="exportinformation/date" />
									</fo:block>
								</fo:table-cell>
							</fo:table-row>
						</fo:table-body>
					</fo:table>
				</fo:static-content>

				<!-- Footer-Block -->
				<fo:static-content flow-name="xsl-region-after">
					<fo:table>
						<fo:table-body>
							<fo:table-row>
								<fo:table-cell background-color="#DEDEDE" padding="4px">
									<!-- Page Numbering -->
									<fo:block font-size="10pt" text-align="center">
										Page &#x0020;
										<fo:page-number />
										of
										<fo:page-number-citation-last ref-id="total"/>
									</fo:block>
								</fo:table-cell>
							</fo:table-row>
							<fo:table-row>
								<fo:table-cell padding="2px">
									<fo:block font-size="8pt">Created with A-STPA</fo:block>
								</fo:table-cell>
							</fo:table-row>
						</fo:table-body>
					</fo:table>
				</fo:static-content>

				<fo:flow flow-name="xsl-region-body">
					<!-- *************** Accidents *************** -->
					<fo:block>
						<fo:block font-size="24pt" space-after="5pt" page-break-after="avoid">
							Accidents
						</fo:block>
						<!-- Accidents-Table-Template -->
						<xsl:call-template name="accidentsTable"/>
					</fo:block>


					<!-- *************** Hazard table *************** -->
					<fo:block>
						<fo:block font-size="24pt" space-after="5pt" page-break-after="avoid">
							Hazards
						</fo:block>
						<!-- Hazard-Table-Template -->
						<xsl:call-template name="hazardTable"/>
					</fo:block>

					<!-- *************** Safety Constraints table *************** -->
					<fo:block>
						<fo:block font-size="24pt" space-after="5pt" page-break-after="avoid">
							Safety Constraints
						</fo:block>
						<!-- Safety Constraint-Table-Template -->
						<xsl:call-template name="safetyConstraintsTable"/>
					</fo:block>

					<!-- *************** System Goals table *************** -->
					<fo:block>
						<fo:block font-size="24pt" space-after="5pt" page-break-after="avoid">
							System Goals
						</fo:block>
						<!-- System Goals-Table-Template -->
						<xsl:call-template name="systemGoalsTable"/>
					</fo:block>

					<!-- *************** Design Requirements table *************** -->
					<fo:block page-break-after="always">
						<fo:block font-size="24pt" space-after="5pt" page-break-after="avoid">
							Design Requirements
						</fo:block>
						<!-- Design Requirements-Table-Template -->
						<xsl:call-template name="designRequirementsTable"/>
					</fo:block>
					
					<!-- *************** Control Structure Diagram *************** -->
					<fo:block page-break-after="always">
						<fo:block font-size="24pt" space-after="5pt" page-break-after="avoid">
							Control Structure Diagram
						</fo:block>
						<fo:block>
						<xsl:if test="exportinformation/csImagePath">
							<fo:external-graphic content-width="190mm">
								<xsl:attribute name="src">
								<!-- Path of the Control Structure via haz-file -->
								<xsl:value-of select="exportinformation/csImagePath" />
							</xsl:attribute>
							</fo:external-graphic>
						</xsl:if>	
						</fo:block>
					</fo:block>


					<!-- *************** Control Actions table *************** -->
					<fo:block>
						<fo:block font-size="24pt" space-after="5pt" page-break-after="avoid">
							Control Actions
						</fo:block>
						<!-- Control Actions-Table-Template -->
						<xsl:call-template name="controlActionsTable"/>
					</fo:block>

					<!-- *************** Unsafe Control Actions *************** -->
					<fo:block>
						<fo:block font-size="24pt" space-after="5pt" page-break-after="avoid">
							Unsafe Control Actions
						</fo:block>
						<!-- Unsafe Control Actions-Table-Template -->
						<xsl:call-template name="ucaTable"/>
					</fo:block>

					<!-- *************** Corresponding Safety Constraints *************** -->
					<fo:block page-break-after="always">
						<fo:block font-size="24pt" space-after="5pt" page-break-after="avoid">
							Corresponding Safety
							Constraints
						</fo:block>
						<!-- Unsafe Control Actions-Table-Template -->
						<xsl:call-template name="correspondingSafetyConstraintsTable"/>
					</fo:block>


					<!-- *************** Control Structure Diagram with Process Model ***************-->
					<fo:block page-break-after="always">
						<fo:block font-size="24pt" space-after="5pt" page-break-after="avoid">
							Control Structure Diagram
							with Process Model
						</fo:block>
						<fo:block>
						<xsl:if test="exportinformation/cspmImagePath">
							<fo:external-graphic content-width="190mm">
								<xsl:attribute name="src">
								<!-- Path of the Control Structure with Process-Model via haz-file -->
								<xsl:value-of select="exportinformation/cspmImagePath" />
							</xsl:attribute>
							</fo:external-graphic>
						</xsl:if>
						</fo:block>
					</fo:block>

					<!-- *************** Causal Factors Table *************** -->
					<fo:block>
						<fo:block font-size="24pt" space-after="5pt" page-break-after="avoid">
							Causal Factors
						</fo:block>
						<!-- Causal Factors-Table-Template -->
						<xsl:call-template name="causalFactorsTable"/>
					</fo:block>
				</fo:flow>
			</fo:page-sequence>
			
			<!-- ++++++++++++++++++++++++++++++++++++++++++++++++++++++ -->
			<!-- +++++++++++++++++++++ END OF PDF +++++++++++++++++++++ -->
			<!-- ++++++++++++++++++++++++++++++++++++++++++++++++++++++ -->

		</fo:root>
	</xsl:template>

	<!-- ############################################################-->	
	<!-- ################### Templates of the PDF ###################-->
	<!-- ############################################################-->
	
	<!-- ################### Heading-Background-Color ################### -->
	<xsl:template name="headTheme">
		<xsl:if test="exportinformation/backgroundColor">
			<xsl:attribute name="background-color">
				<xsl:value-of select="exportinformation/backgroundColor" />
			</xsl:attribute>
		</xsl:if>
	</xsl:template>
	
	<!-- ################### Font-Color ################### -->
	<xsl:template name="fontTheme">
		<xsl:if test="exportinformation/fontColor">
			<xsl:attribute name="color">
				<xsl:value-of select="exportinformation/fontColor" />
			</xsl:attribute>
		</xsl:if>
	</xsl:template>
	
	<xsl:template name="hazardLinkColor">
		<xsl:choose>
			<xsl:when test="links = 'Not Hazardous'">
				<fo:block color="#2D7500">
					<xsl:value-of select="links" />
				</fo:block>
			</xsl:when>
			<xsl:otherwise>
				<fo:block color="#820000">
					<xsl:value-of select="links" />
				</fo:block>
			</xsl:otherwise>
		</xsl:choose>
	</xsl:template>
	
	<!-- ################### Accidents-Table ################### -->
	<xsl:template name="accidentsTable">
		<fo:table border="none" space-after="30pt">
			<fo:table-column column-number="1" column-width="5%"
				border-style="none" />
			<fo:table-column column-number="2" column-width="30%"
				border-style="none" />
			<fo:table-column column-number="3" column-width="50%"
				border-style="none" />
			<fo:table-column column-number="4" column-width="15%"
				border-style="none" />
			<fo:table-header border="none" background-color="#1A277A"
				color="#FFFFFF" padding="3px">
				<xsl:call-template name="headTheme"/>
				<xsl:call-template name="fontTheme"/>
				<fo:table-row>
					<fo:table-cell padding="3px">
						<fo:block font-weight="bold">No.</fo:block>
					</fo:table-cell>
					<fo:table-cell padding="3px">
						<fo:block font-weight="bold">Title</fo:block>
					</fo:table-cell>
					<fo:table-cell padding="3px">
						<fo:block font-weight="bold">Description</fo:block>
					</fo:table-cell>
					<fo:table-cell padding="3px">
						<fo:block font-weight="bold">Related Hazards</fo:block>
					</fo:table-cell>
				</fo:table-row>
			</fo:table-header>
		
			<fo:table-body>
				<xsl:choose>
					<!-- Checks whether some accidents are defined -->
					<xsl:when test="hazacc/accidents/accident">
						<xsl:for-each select="hazacc/accidents/accident">
							<fo:table-row border="none">
								<xsl:if test="position() mod 2 = 0">
									<xsl:attribute name="background-color">#D9D9D9</xsl:attribute>
								</xsl:if>
								<fo:table-cell padding="3px">
									<fo:block font-size="12pt">
										<xsl:value-of select="number" />
									</fo:block>
								</fo:table-cell>
								<fo:table-cell padding="3px">
									<fo:block font-size="12pt">
										<xsl:value-of select="title" />
									</fo:block>
								</fo:table-cell>
								<fo:table-cell padding="3px">
									<fo:block font-size="12pt">
										<xsl:value-of select="description" />
									</fo:block>
								</fo:table-cell>
								<fo:table-cell padding="3px">
									<fo:block font-size="12pt">
										<xsl:value-of select="links" />
									</fo:block>
								</fo:table-cell>
							</fo:table-row>
						</xsl:for-each>
					</xsl:when>
					<!-- If there are no accidents defined the first row -->
					<!-- should be filled with strokes -->
					<xsl:otherwise>
						<fo:table-row>
							<fo:table-cell padding="3px">
								<fo:block>
									&#x2014; </fo:block>
							</fo:table-cell>
							<fo:table-cell padding="3px">
								<fo:block>
									&#x2014; </fo:block>
							</fo:table-cell>
							<fo:table-cell padding="3px">
								<fo:block>
									&#x2014; </fo:block>
							</fo:table-cell>
							<fo:table-cell padding="3px">
								<fo:block>
									&#x2014; </fo:block>
							</fo:table-cell>
						</fo:table-row>
					</xsl:otherwise>
				</xsl:choose>
			</fo:table-body>
		</fo:table>
	</xsl:template>
	
	<!-- ################### Hazard-Table ################### -->
	<xsl:template name="hazardTable">
	<fo:table border="none" space-after="30pt">
		<fo:table-column column-number="1" column-width="5%"
			border-style="none" />
		<fo:table-column column-number="2" column-width="30%"
			border-style="none" />
		<fo:table-column column-number="3" column-width="50%"
			border-style="none" />
		<fo:table-column column-number="4" column-width="15%"
			border-style="none" />
		
		<fo:table-header border="none" background-color="#1A277A"
			color="#FFFFFF" padding="3px">
			<!-- Sets the PDF-Theme-Color -->
				<xsl:call-template name="headTheme"/>
				<xsl:call-template name="fontTheme"/>
				<fo:table-row>
					<fo:table-cell padding="3px">
						<fo:block font-weight="bold">No.</fo:block>
					</fo:table-cell>
					<fo:table-cell padding="3px">
						<fo:block font-weight="bold">Title</fo:block>
					</fo:table-cell>
					<fo:table-cell padding="3px">
						<fo:block font-weight="bold">Description</fo:block>
					</fo:table-cell>
					<fo:table-cell padding="3px">
						<fo:block font-weight="bold">Related Accidents</fo:block>
					</fo:table-cell>
				</fo:table-row>
			</fo:table-header>
		
			<fo:table-body>
				<xsl:choose>
					<!-- Checks whether some hazards are defined -->
					<xsl:when test="hazacc/hazards/hazard">
						<xsl:for-each select="hazacc/hazards/hazard">
							<fo:table-row border="none">
								<xsl:if test="position() mod 2 = 0">
									<xsl:attribute name="background-color">
					#D9D9D9
					</xsl:attribute>
								</xsl:if>
								<fo:table-cell padding="3px">
									<fo:block font-size="12pt">
										<xsl:value-of select="number" />
									</fo:block>
								</fo:table-cell>
								<fo:table-cell padding="3px">
									<fo:block font-size="12pt">
										<xsl:value-of select="title" />
									</fo:block>
								</fo:table-cell>
								<fo:table-cell padding="3px">
									<fo:block font-size="12pt">
										<xsl:value-of select="description" />
									</fo:block>
								</fo:table-cell>
								<fo:table-cell padding="3px">
									<fo:block font-size="12pt">
										<xsl:value-of select="links" />
									</fo:block>
								</fo:table-cell>
							</fo:table-row>
						</xsl:for-each>
					</xsl:when>
					<!-- If there are no hazards defined the first row -->
					<!-- should be filled with strokes -->
					<xsl:otherwise>
						<fo:table-row>
							<fo:table-cell padding="3px">
								<fo:block>
									&#x2014; </fo:block>
							</fo:table-cell>
							<fo:table-cell padding="3px">
								<fo:block>
									&#x2014; </fo:block>
							</fo:table-cell>
							<fo:table-cell padding="3px">
								<fo:block>
									&#x2014; </fo:block>
							</fo:table-cell>
							<fo:table-cell padding="3px">
								<fo:block>
									&#x2014; </fo:block>
							</fo:table-cell>
						</fo:table-row>
					</xsl:otherwise>
				</xsl:choose>
			</fo:table-body>
		</fo:table>
	</xsl:template>
	
	<!-- ################### Safety Constraints Table ################### -->
	<xsl:template name="safetyConstraintsTable">
	<fo:table border="none" space-after="30pt">
		<fo:table-column column-number="1" column-width="5%"
			border-style="none" />
		<fo:table-column column-number="2" column-width="30%"
			border-style="none" />
		<fo:table-column column-number="3" column-width="65%"
			border-style="none" />
		
		<fo:table-header border="none" background-color="#1A277A"
			color="#FFFFFF">
				<!-- Sets the PDF-Theme-Color -->
				<xsl:call-template name="headTheme"/>
				<xsl:call-template name="fontTheme"/>
				<fo:table-row>
					<fo:table-cell padding="3px">
						<fo:block font-weight="bold">No.</fo:block>
					</fo:table-cell>
					<fo:table-cell padding="3px">
						<fo:block font-weight="bold">Safety Constraint</fo:block>
					</fo:table-cell>
					<fo:table-cell padding="3px">
						<fo:block font-weight="bold">Description</fo:block>
					</fo:table-cell>
				</fo:table-row>
			</fo:table-header>
		
			<fo:table-body>
				<xsl:choose>
					<!-- Checks whether some Safety Constraints are defined -->
					<xsl:when test="sds/safetyConstraints/safetyConstraint">
						<xsl:for-each select="sds/safetyConstraints/safetyConstraint">
							<fo:table-row border="none">
								<xsl:if test="position() mod 2 = 0">
									<xsl:attribute name="background-color">
					#D9D9D9
					</xsl:attribute>
								</xsl:if>
								<fo:table-cell padding="3px">
									<fo:block font-size="12pt">
										<xsl:value-of select="number" />
									</fo:block>
								</fo:table-cell>
								<fo:table-cell padding="3px">
									<fo:block font-size="12pt">
										<xsl:value-of select="title" />
									</fo:block>
								</fo:table-cell>
								<fo:table-cell padding="3px">
									<fo:block font-size="12pt">
										<xsl:value-of select="description" />
									</fo:block>
								</fo:table-cell>
							</fo:table-row>
						</xsl:for-each>
					</xsl:when>
					<!-- If there are no Safety Constraints defined the first row -->
					<!-- should be filled with strokes -->
					<xsl:otherwise>
						<fo:table-row>
							<fo:table-cell padding="3px">
								<fo:block>
									&#x2014; </fo:block>
							</fo:table-cell>
							<fo:table-cell padding="3px">
								<fo:block>
									&#x2014; </fo:block>
							</fo:table-cell>
							<fo:table-cell padding="3px">
								<fo:block>
									&#x2014; </fo:block>
							</fo:table-cell>
						</fo:table-row>
					</xsl:otherwise>
				</xsl:choose>
			</fo:table-body>
		</fo:table>
	</xsl:template>	
	
	<!-- ################### System Goals Table ################### -->
	<xsl:template name="systemGoalsTable">
	<fo:table border="none" space-after="30pt">
		<fo:table-column column-number="1" column-width="5%"
			border-style="none" />
		<fo:table-column column-number="2" column-width="30%"
			border-style="none" />
		<fo:table-column column-number="3" column-width="65%"
			border-style="none" />
		
		<fo:table-header border="none" background-color="#1A277A"
			color="#FFFFFF">
			<!-- Sets the PDF-Theme-Color -->
				<xsl:call-template name="headTheme"/>
				<xsl:call-template name="fontTheme"/>
				<fo:table-row>
					<fo:table-cell padding="3px">
						<fo:block font-weight="bold">No.</fo:block>
					</fo:table-cell>
					<fo:table-cell padding="3px">
						<fo:block font-weight="bold">System Goal</fo:block>
					</fo:table-cell>
					<fo:table-cell padding="3px">
						<fo:block font-weight="bold">Description</fo:block>
					</fo:table-cell>
				</fo:table-row>
			</fo:table-header>
		
			<fo:table-body>
				<xsl:choose>
					<!-- Checks whether some System Goals are defined -->
					<xsl:when test="sds/systemGoals/systemGoal">
						<xsl:for-each select="sds/systemGoals/systemGoal">
							<fo:table-row border="none">
								<xsl:if test="position() mod 2 = 0">
									<xsl:attribute name="background-color">
					#D9D9D9
					</xsl:attribute>
								</xsl:if>
								<fo:table-cell padding="3px">
									<fo:block font-size="12pt">
										<xsl:value-of select="number" />
									</fo:block>
								</fo:table-cell>
								<fo:table-cell padding="3px">
									<fo:block font-size="12pt">
										<xsl:value-of select="title" />
									</fo:block>
								</fo:table-cell>
								<fo:table-cell padding="3px">
									<fo:block font-size="12pt">
										<xsl:value-of select="description" />
									</fo:block>
								</fo:table-cell>
							</fo:table-row>
						</xsl:for-each>
					</xsl:when>
					<!-- If there are no System Goals defined the first row -->
					<!-- should be filled with strokes -->
					<xsl:otherwise>
						<fo:table-row>
							<fo:table-cell padding="3px">
								<fo:block>
									&#x2014; </fo:block>
							</fo:table-cell>
							<fo:table-cell padding="3px">
								<fo:block>
									&#x2014; </fo:block>
							</fo:table-cell>
							<fo:table-cell padding="3px">
								<fo:block>
									&#x2014; </fo:block>
							</fo:table-cell>
						</fo:table-row>
					</xsl:otherwise>
				</xsl:choose>
			</fo:table-body>
		</fo:table>
	</xsl:template>
	
	<!-- ################### Design Requirements Table ################### -->
	<xsl:template name="designRequirementsTable">
		<fo:table border="none" space-after="30pt">
		<fo:table-column column-number="1" column-width="5%"
		border-style="none" />
		<fo:table-column column-number="2" column-width="30%"
			border-style="none" />
		<fo:table-column column-number="3" column-width="65%"
			border-style="none" />
	
		<fo:table-header border="none" background-color="#1A277A"
			color="#FFFFFF">
			<!-- Sets the PDF-Theme-Color -->
			<xsl:call-template name="headTheme" />
			<xsl:call-template name="fontTheme" />
			<fo:table-row>
				<fo:table-cell padding="3px">
					<fo:block font-weight="bold">No.</fo:block>
				</fo:table-cell>
				<fo:table-cell padding="3px">
					<fo:block font-weight="bold">Design Requirement</fo:block>
				</fo:table-cell>
				<fo:table-cell padding="3px">
					<fo:block font-weight="bold">Description</fo:block>
				</fo:table-cell>
			</fo:table-row>
		</fo:table-header>
	
		<fo:table-body>
			<xsl:choose>
				<!-- Checks whether some Design Requirements are defined -->
				<xsl:when test="sds/designRequirements/designRequirement">
					<xsl:for-each select="sds/designRequirements/designRequirement">
						<fo:table-row border="none">
							<xsl:if test="position() mod 2 = 0">
								<xsl:attribute name="background-color">
					#D9D9D9
					</xsl:attribute>
							</xsl:if>
							<fo:table-cell padding="3px">
								<fo:block font-size="12pt">
									<xsl:value-of select="number" />
								</fo:block>
							</fo:table-cell>
							<fo:table-cell padding="3px">
								<fo:block font-size="12pt">
									<xsl:value-of select="title" />
								</fo:block>
							</fo:table-cell>
							<fo:table-cell padding="3px">
								<fo:block font-size="12pt">
									<xsl:value-of select="description" />
								</fo:block>
							</fo:table-cell>
						</fo:table-row>
					</xsl:for-each>
				</xsl:when>
				<!-- If there are no Design Requirements defined the first row -->
				<!-- should be filled with strokes -->
				<xsl:otherwise>
					<fo:table-row>
						<fo:table-cell padding="3px">
							<fo:block>
								&#x2014; </fo:block>
						</fo:table-cell>
						<fo:table-cell padding="3px">
							<fo:block>
								&#x2014; </fo:block>
						</fo:table-cell>
						<fo:table-cell padding="3px">
							<fo:block>
								&#x2014; </fo:block>
						</fo:table-cell>
					</fo:table-row>
				</xsl:otherwise>
			</xsl:choose>
		</fo:table-body>
		</fo:table>
	</xsl:template>
	
	<!-- ################### Control Actions Table ################### -->
	<xsl:template name="controlActionsTable">
		<fo:table border="none" space-after="30pt">
			<fo:table-column column-number="1" column-width="5%"
				border-style="none" />
			<fo:table-column column-number="2" column-width="30%"
				border-style="none" />
			<fo:table-column column-number="3" column-width="65%"
				border-style="none" />
			<fo:table-header border="none" background-color="#1A277A"
				color="#FFFFFF">
					<!-- Sets the PDF-Theme-Color -->
					<xsl:call-template name="headTheme"/>
					<xsl:call-template name="fontTheme"/>
				<fo:table-row>
					<fo:table-cell padding="3px">
						<fo:block font-weight="bold">No.</fo:block>
					</fo:table-cell>
					<fo:table-cell padding="3px">
						<fo:block font-weight="bold">Control Action</fo:block>
					</fo:table-cell>
					<fo:table-cell padding="3px">
						<fo:block font-weight="bold">Description</fo:block>
					</fo:table-cell>
				</fo:table-row>
			</fo:table-header>
		
			<fo:table-body>
				<xsl:choose>
					<!-- Checks whether there are some Control Actions defined -->
					<xsl:when test="cac/controlactions/controlaction">
						<xsl:for-each select="cac/controlactions/controlaction">
							<fo:table-row border="none">
								<xsl:if test="position() mod 2 = 0">
									<xsl:attribute name="background-color">
					#D9D9D9
					</xsl:attribute>
								</xsl:if>
								<fo:table-cell padding="3px">
									<fo:block font-size="12pt">
										<xsl:value-of select="number" />
									</fo:block>
								</fo:table-cell>
								<fo:table-cell padding="3px">
									<fo:block font-size="12pt">
										<xsl:value-of select="title" />
									</fo:block>
								</fo:table-cell>
								<fo:table-cell padding="3px">
									<fo:block font-size="12pt">
										<xsl:value-of select="description" />
									</fo:block>
								</fo:table-cell>
							</fo:table-row>
						</xsl:for-each>
					</xsl:when>
					<!-- If there are no Control Actions defined the first row -->
					<!-- should be filled with strokes -->
					<xsl:otherwise>
						<fo:table-row>
							<fo:table-cell padding="3px">
								<fo:block>
									&#x2014; </fo:block>
							</fo:table-cell>
							<fo:table-cell padding="3px">
								<fo:block>
									&#x2014; </fo:block>
							</fo:table-cell>
							<fo:table-cell padding="3px">
								<fo:block>
									&#x2014; </fo:block>
							</fo:table-cell>
						</fo:table-row>
					</xsl:otherwise>
				</xsl:choose>
			</fo:table-body>
		</fo:table>
	</xsl:template>
	
	<!-- ################### Unsafe Control Actions Table (UCA) ################### -->
	<xsl:template name="ucaTable">
		<fo:table border="none" space-after="30pt">
			<fo:table-column column-number="1" column-width="20%"
				border-style="none" />
			<fo:table-column column-number="2" column-width="20%"
				border-style="none" />
			<fo:table-column column-number="3" column-width="20%"
				border-style="none" />
			<fo:table-column column-number="4" column-width="20%"
				border-style="none" />
			<fo:table-column column-number="5" column-width="20%"
				border-style="none" />
			<fo:table-header border="none" background-color="#1A277A"
				color="#FFFFFF">
				<!-- Sets the PDF-Theme-Color -->
				<xsl:call-template name="headTheme"/>
				<xsl:call-template name="fontTheme"/>
				<fo:table-row>
					<fo:table-cell padding="3px">
						<fo:block font-weight="bold">Control Action</fo:block>
					</fo:table-cell>
					<fo:table-cell padding="3px">
						<fo:block font-weight="bold">Not Given</fo:block>
					</fo:table-cell>
					<fo:table-cell padding="3px">
						<fo:block font-weight="bold">Given Incorrectly</fo:block>
					</fo:table-cell>
					<fo:table-cell padding="3px">
						<fo:block font-weight="bold">Wrong Timing or Order</fo:block>
					</fo:table-cell>
					<fo:table-cell padding="3px">
						<fo:block font-weight="bold">Stopped too soon or applied too long
						</fo:block>
					</fo:table-cell>
				</fo:table-row>
			</fo:table-header>
			
			<fo:table-body>
				<xsl:choose>
					<!-- Checks if there are some Control Actions already defined -->
					<xsl:when test="cac/controlactions/controlaction">
						<xsl:for-each select="cac/controlactions/controlaction">
							<fo:table-row border-bottom="2pt solid black"
								border-top="2pt solid black">
								<fo:table-cell padding="4px" background-color="#FFFFFF"
									color="#000000" border-right="2pt solid black">
									<fo:block font-size="12pt" font-weight="bold">
										<xsl:value-of select="title" />
									</fo:block>
								</fo:table-cell>
								
								<!-- Creates for each Control Action a Unsafe Control Action Cell -->
								
								<!-- NOT GIVEN-Cell -->
								<fo:table-cell padding="4px" background-color="#FFFFFF">
									<!-- NOT GIVEN - SubTable -->
									<xsl:call-template name="notGiven"/>
								</fo:table-cell>
			
								<!-- GIVEN INCORRECTLY-Cell -->
								<fo:table-cell padding="4px" background-color="#FFFFFF">
									<!-- GIVEN_INCORRECTLY - SubTable -->
									<xsl:call-template name="givenIncorrectly"/>
								</fo:table-cell>
			
								<!-- ########## WRONG TIMING -Cell ########## -->
								<fo:table-cell padding="4px" background-color="#FFFFFF">
									<!-- WRONG TIMING - SubTable -->
									<xsl:call-template name="wrongTiming"/>
								</fo:table-cell>
			
								<!-- ########## STOPPED TOO SOON-Cell ########## -->
								<fo:table-cell padding="4px">
									<!-- STOPPED TOO SOON - SubTable -->
									<xsl:call-template name="stoppedTooSoon"/>
									</fo:table-cell>
								</fo:table-row>
							</xsl:for-each>
						</xsl:when>
						
						<!-- If there are no Control Actions defined the first row -->
						<!-- should be filled with strokes -->
						<xsl:otherwise>
							<fo:table-row>
								<fo:table-cell padding="3px">
									<fo:block>
										&#x2014; </fo:block>
								</fo:table-cell>
								<fo:table-cell padding="3px">
									<fo:block>
										&#x2014; </fo:block>
								</fo:table-cell>
								<fo:table-cell padding="3px">
									<fo:block>
										&#x2014; </fo:block>
								</fo:table-cell>
								<fo:table-cell padding="3px">
									<fo:block>
										&#x2014; </fo:block>
								</fo:table-cell>
								<fo:table-cell padding="3px">
									<fo:block>
										&#x2014; </fo:block>
								</fo:table-cell>
							</fo:table-row>
						</xsl:otherwise>
					</xsl:choose>
				</fo:table-body>
			</fo:table>
	</xsl:template>
	
	<!-- ################### UCA-Sub-Table- type: NOT GIVEN ################### -->
	<xsl:template name="notGiven">
		<fo:table>
			<fo:table-column column-width="100%" />
			<fo:table-body>
				<!-- empty row -->
				<fo:table-row>
					<fo:table-cell>
						<fo:block/>	
					</fo:table-cell>
				</fo:table-row>
				<xsl:for-each
					select="unsafecontrolactions/unsafecontrolaction[type='NOT_GIVEN']">
					<fo:table-row>
						<!-- Sets the row-colour -->
						<xsl:if test="position() mod 2 = 0">
							<xsl:attribute name="background-color">
					#D9D9D9
					</xsl:attribute>
						</xsl:if>
						<fo:table-cell padding="3px">
							<fo:block>
								<xsl:value-of select="description" />
								<!-- Chooses the Link-Colour depending on the hazardous-state -->
								<xsl:call-template name="ucaHazLinkColor"/>
							</fo:block>
						</fo:table-cell>
					</fo:table-row>
				</xsl:for-each>
			</fo:table-body>
		</fo:table>
	</xsl:template>
	
	<!-- ################### UCA-Sub-Table- type: GIVEN INCORRECTLY ################### -->
	<xsl:template name="givenIncorrectly">
	<fo:table>
		<fo:table-column column-width="100%" />
		<!-- empty row -->
		<fo:table-body>
			<fo:table-row>
				<fo:table-cell>
					<fo:block/>		
				</fo:table-cell>
			</fo:table-row>
			<!-- For each "GIVEN_INCORRECTLY"-UCA, a new row shall be generated -->
			<xsl:for-each
				select="unsafecontrolactions/unsafecontrolaction[type='GIVEN_INCORRECTLY']">
				<fo:table-row>
					<!-- Sets the row-colour -->
					<xsl:if test="position() mod 2 = 0">
						<xsl:attribute name="background-color">
				#D9D9D9
				</xsl:attribute>
					</xsl:if>
					<fo:table-cell padding="3px">
						<fo:block>
							<xsl:value-of select="description" />
							<!-- Chooses the Link-Colour depending on the hazardous-state -->
							<xsl:call-template name="ucaHazLinkColor"/>
						</fo:block>
					</fo:table-cell>
				</fo:table-row>
			</xsl:for-each>
		</fo:table-body>
	</fo:table>
	</xsl:template>
	
	<!-- ################### UCA-Sub-Table- type: WRONG TIMING ################### -->
	<xsl:template name="wrongTiming">
		<fo:table>
			<fo:table-column column-width="100%" />
			<fo:table-body>
				<!-- empty row -->
				<fo:table-row>
					<fo:table-cell>
						<fo:block/>
	
						
					</fo:table-cell>
				</fo:table-row>
				<!-- For each "WRONG-TIMING"-UCA, a new row shall be generated -->
				<xsl:for-each
					select="unsafecontrolactions/unsafecontrolaction[type='WRONG_TIMING']">
					<fo:table-row>
						<!-- Sets the row-colour -->
						<xsl:if test="position() mod 2 = 0">
							<xsl:attribute name="background-color">
					#D9D9D9
					</xsl:attribute>
						</xsl:if>
						<fo:table-cell padding="3px">
							<fo:block>
								<xsl:value-of select="description" />
								<!-- Chooses the Link-Colour depending on the hazardous-state -->
								<xsl:call-template name="ucaHazLinkColor"/>
							</fo:block>
						</fo:table-cell>
					</fo:table-row>
				</xsl:for-each>
			</fo:table-body>
		</fo:table>
	</xsl:template>
	
	<!-- ################### UCA-Sub-Table- type: STOPPED TOO SOON ################### -->
	<xsl:template name="stoppedTooSoon">
		<fo:table>
			<fo:table-column column-width="100%" />
			<fo:table-body>
				<!-- empty Row -->
				<fo:table-row>
					<fo:table-cell>
						<fo:block/>
					</fo:table-cell>
				</fo:table-row>
				<!-- For each "STOPPED TOO SOON"-UCA, a new row shall be generated -->
				<xsl:for-each
					select="unsafecontrolactions/unsafecontrolaction[type='STOPPED_TOO_SOON']">
					<fo:table-row>
						<!-- Sets the row-colour -->
							<xsl:if test="position() mod 2 = 0">
								<xsl:attribute name="background-color">
						#D9D9D9
						</xsl:attribute>
							</xsl:if>
							<fo:table-cell padding="3px">
								<fo:block>
									<xsl:value-of select="description" />
								<!-- Chooses the Link-Colour depending on the hazardous-state -->
								<xsl:call-template name="ucaHazLinkColor"/>
								</fo:block>
							</fo:table-cell>
						</fo:table-row>
					</xsl:for-each>
				</fo:table-body>
			</fo:table>
	</xsl:template>
	
	<!-- ################### UCA - Hazardous-Color-Chooser ################### -->
	<xsl:template name="ucaHazLinkColor">
		<xsl:choose>
			<xsl:when test="links = 'Not Hazardous'">
				<fo:block color="#2D7500">
					&#x005B;
					<xsl:value-of select="links" />
					&#x005D;
				</fo:block>
			</xsl:when>
			<xsl:otherwise>
				<fo:block color="#820000">
					&#x005B;
					<xsl:value-of select="links" />
					&#x005D;
				</fo:block>
			</xsl:otherwise>
		</xsl:choose>
	</xsl:template>
	
	<!-- ################### Corresponding Safety Constraints Table ################### -->
	<xsl:template name="correspondingSafetyConstraintsTable">
		<fo:table border="none" space-after="30pt">
			<fo:table-column column-number="1" column-width="50%"
				border-style="none" />
			<fo:table-column column-number="2" column-width="50%"
				border-style="none" />
			<fo:table-header border="none" background-color="#1A277A"
				color="#FFFFFF">
					<!-- Sets the PDF-Theme-Color -->
					<xsl:call-template name="headTheme"/>
					<xsl:call-template name="fontTheme"/>
				<fo:table-row>
					<fo:table-cell padding="3px">
						<fo:block font-weight="bold">Unsafe Control Actions</fo:block>
					</fo:table-cell>
					<fo:table-cell padding="3px">
						<fo:block font-weight="bold">Resulting Safety Constraints</fo:block>
					</fo:table-cell>
				</fo:table-row>
			</fo:table-header>
			
			<fo:table-body>
				<xsl:choose>
					<!-- Checks for hazardous UCA -->
					<xsl:when
						test="cac/controlactions/controlaction/unsafecontrolactions/unsafecontrolaction/correspondingSafetyConstraint/text != ''">
						<xsl:for-each
							select="cac/controlactions/controlaction/unsafecontrolactions/unsafecontrolaction[links != 'Not Hazardous']">
	
								<fo:table-row border="none">
									<xsl:if test="position() mod 2 = 0">
										<xsl:attribute name="background-color">#D9D9D9</xsl:attribute>
									</xsl:if>
									<fo:table-cell padding="3px">
										<fo:block font-size="12pt">
											<xsl:value-of select="description" />
										</fo:block>
									</fo:table-cell>
									<fo:table-cell padding="3px">
										<fo:block font-size="12pt">
											<xsl:value-of select="correspondingSafetyConstraint/text" />
										</fo:block>
									</fo:table-cell>
								</fo:table-row>						
						</xsl:for-each>
					</xsl:when>
					<!-- If there are no hazardous UCA defined the first row -->
					<!-- should be filled with strokes -->
					<xsl:otherwise>
						<fo:table-row>
							<fo:table-cell padding="3px">
								<fo:block>
									&#x2014; </fo:block>
							</fo:table-cell>
							<fo:table-cell padding="3px">
								<fo:block>
									&#x2014; </fo:block>
							</fo:table-cell>
						</fo:table-row>
					</xsl:otherwise>
				</xsl:choose>
			</fo:table-body>
		</fo:table>
	</xsl:template>
	
	<!-- ################### Causal Factors Table ################### -->
	<xsl:template name="causalFactorsTable">
		<fo:table border="none" space-after="30pt">
			<fo:table-column column-number="1" column-width="20%"
				border-style="none" />
			<fo:table-column column-number="2" column-width="80%"
				border-style="none" />
			<fo:table-header border="none" background-color="#1A277A"
				color="#FFFFFF" padding="3px">
					<!-- Sets the PDF-Theme-Color -->
					<xsl:call-template name="headTheme"/>
					<xsl:call-template name="fontTheme"/>
				<fo:table-row>
					<fo:table-cell padding="3px">
						<fo:block font-weight="bold">Component</fo:block>
					</fo:table-cell>
					<fo:table-cell padding="3px">
						<fo:block font-weight="bold">
							<fo:table border="none" space-after="30pt">
								<fo:table-column column-number="1" column-width="25%"
									border-style="none" />
								<fo:table-column column-number="2" column-width="25%"
									border-style="none" />
								<fo:table-column column-number="3" column-width="25%"
									border-style="none" />
								<fo:table-column column-number="4" column-width="25%"
									border-style="none" />
	
								<fo:table-header border="none" background-color="#1A277A"
									color="#FFFFFF">
									<!-- Sets the PDF-Theme-Color -->
									<xsl:call-template name="headTheme"/>
									<xsl:call-template name="fontTheme"/>
									<fo:table-row>
										<fo:table-cell>
											<fo:block font-weight="bold">Causal Factor</fo:block>
										</fo:table-cell>
										<fo:table-cell>
											<fo:block font-weight="bold">Hazard Links</fo:block>
										</fo:table-cell>
										<fo:table-cell>
											<fo:block font-weight="bold">Safety Constraint</fo:block>
										</fo:table-cell>
										<fo:table-cell>
											<fo:block font-weight="bold">Notes</fo:block>
										</fo:table-cell>
									</fo:table-row>
								</fo:table-header>
								<fo:table-body>
									<!-- Empty Table-Body -->
									<fo:table-row>
										<fo:table-cell>
											<fo:block/>
										</fo:table-cell>
										<fo:table-cell>
											<fo:block/>
										</fo:table-cell>
										<fo:table-cell>
											<fo:block/>
										</fo:table-cell>
									</fo:table-row>
								</fo:table-body>
							</fo:table>
						</fo:block>
					</fo:table-cell>
				</fo:table-row>
			</fo:table-header>
	
			<fo:table-body>
				<xsl:choose>
				<!-- Checks if there are some components for the CausalFactors-Table -->
					<xsl:when test="controlstructure/component/children/component">
						<xsl:for-each select="controlstructure/component/children/component[componentType != 'TEXTFIELD']">
							<fo:table-row border-bottom="2pt solid black"
								border-top="2pt solid black">
								<fo:table-cell padding="4px" background-color="#FFFFFF"
									color="#000000" border-right="2pt solid black">
									<fo:block font-size="12pt" font-weight="bold">
										<xsl:value-of select="text" />
									</fo:block>
								</fo:table-cell>
								<fo:table-cell>
								
									<!-- ***** CausalFactors with its relatives ***** -->
									<fo:block font-size="12pt">
									<xsl:call-template name="causalFactorRelatives"/>
									</fo:block>
								</fo:table-cell>
							</fo:table-row>
						</xsl:for-each>
					</xsl:when>
					<!-- If there are no CausalFactor-Components defined the first row -->
					<!-- should be filled with strokes -->
					<xsl:otherwise>
						<fo:table-row>
							<fo:table-cell padding="3px">
								<fo:block>
									&#x2014; </fo:block>
							</fo:table-cell>
							<fo:table-cell padding="3px">
								<fo:block>
									&#x2014; </fo:block>
							</fo:table-cell>
						</fo:table-row>
					</xsl:otherwise>
				</xsl:choose>
			</fo:table-body>
		</fo:table>
	</xsl:template>
	
	<!-- ################### Causal Factors-Sub-Table ################### -->
	<!-- Every Causal Factor and its relative can be found in this sub-table -->
	<xsl:template name="causalFactorRelatives">
		<fo:table>
			<fo:table-column column-number="1" column-width="25%"
				border-style="none" />
			<fo:table-column column-number="2" column-width="25%"
				border-style="none" />
			<fo:table-column column-number="3" column-width="25%"
				border-style="none" />
			<fo:table-column column-number="4" column-width="25%"
				border-style="none" />
			<fo:table-body>
				<xsl:choose>
					<!-- Checks if there are some CausalFactors defined -->
					<!-- for a component. -->
					<xsl:when test="causalFactors/causalFactor">
					<xsl:for-each
					select="causalFactors/causalFactor">
					<fo:table-row>
						<!-- Sets the row-colour -->
						<xsl:if test="position() mod 2 = 0">
							<xsl:attribute name="background-color">#D9D9D9</xsl:attribute>
						</xsl:if>
						
						<!-- Causal Factor -->
						<fo:table-cell padding="3px">
							<fo:block>
								<xsl:value-of select="text" />
							</fo:block>
						</fo:table-cell>
						
						<!-- Hazard Links -->
						<fo:table-cell padding="3px">
							<!-- Decision of the Text-Color depending on the hazardous-state -->
							<xsl:call-template name="hazardLinkColor"/>
						</fo:table-cell>
						
						<!-- Safety Constraint -->
						<fo:table-cell padding="3px">
						<xsl:choose>
								<xsl:when test="safetyConstraint/text != ''">
									<fo:block>
										<xsl:value-of select="safetyConstraint/text"/>
									</fo:block>																					
								</xsl:when>
								<xsl:otherwise>
									<fo:block>
										&#x2014;
									</fo:block>
								</xsl:otherwise>
							</xsl:choose>	
						</fo:table-cell>
						
						<!-- Notes or Rationale -->
						<fo:table-cell padding="3px">
							<xsl:choose>
								<xsl:when test="note = 'Note'">
									<fo:block>
										&#x2014;
									</fo:block>
								</xsl:when>
								<xsl:otherwise>
									<fo:block>
										<xsl:value-of select="note" />
									</fo:block>
								</xsl:otherwise>
							</xsl:choose>	
						</fo:table-cell>
					</fo:table-row>
				</xsl:for-each>
				</xsl:when>
				
				<xsl:otherwise>
				<fo:table-row>
				<fo:table-cell padding="3px">
			
							<fo:block>
								&#x2014;
							</fo:block>
						</fo:table-cell>
						<fo:table-cell padding="3px">
							<fo:block>
							&#x2014;
							</fo:block>
						</fo:table-cell>
						<fo:table-cell padding="3px">
							<fo:block>
							&#x2014;
							</fo:block>
						</fo:table-cell>
						<fo:table-cell padding="3px">
							<fo:block>
							&#x2014;
							</fo:block>
						</fo:table-cell>
				</fo:table-row>
				</xsl:otherwise>
				</xsl:choose>			
			</fo:table-body>
		</fo:table>
	</xsl:template>
</xsl:stylesheet>

